// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:



    Mux16(a=x,b[0..15]=false,sel=zx,out=x1); //zero x or not
    Not16(in=x1,out=notx); // not x?
    Mux16(a=x1,b=notx,sel=nx,out=x2); //select which x

    Mux16(a=y,b[0..15]=false,sel=zy,out=y1); //same for y
    Not16(in=y1,out=noty);
    Mux16(a=y1,b=noty,sel=ny,out=y2);

    Add16(a=x2,b=y2,out=xplusy); //x+y
    And16(a=x2,b=y2,out=xandy); //x&y
    Mux16(a=xandy,b=xplusy,sel=f,out=oa); //select using f
    
    Not16(in=oa,out=noa); //not output
    Mux16(a=oa,b=noa,sel=no,out=out,out[15]=ng,out[0..7]=halfout1,out[8..15]=halfout2); //select out or not our using no
    
    Or8Way(in=halfout1,out=r1); // r1 1 if any bit in o2 is 1
    Or8Way(in=halfout2,out=r2); // r2 1 if any bit in o3 is 1
    Or(a=r1,b=r2,out=nz); // not zero (nz) 1 if any bit in o2 or o3 is 1
    Not(in=nz,out=zr); // not (not zero) = zero. 1 if all o2 and o3 = 0


}